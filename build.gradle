plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.3'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'jacoco'
}


group 'com.gadelev'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-mail:${springBootVersion}"
    implementation 'org.postgresql:postgresql:42.3.3'
    implementation "org.webjars:jquery:3.6.0"
    implementation "org.webjars:bootstrap:4.6.0"
    implementation "org.webjars:webjars-locator-core:0.46"
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
    implementation 'org.thymeleaf:thymeleaf-spring5:3.0.15.RELEASE'
    implementation 'javax.mail:javax.mail-api:1.6.2'
    // https://mvnrepository.com/artifact/org.liquibase/liquibase-core
    implementation 'org.liquibase:liquibase-core:4.9.0'
    liquibaseRuntime('org.liquibase:liquibase-core:4.9.0')
    liquibaseRuntime('org.postgresql:postgresql:42.3.3')
    implementation 'org.testng:testng:7.3.0'

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "com.h2database:h2:1.4.200"
    testImplementation "junit:junit:4.12"
    testImplementation 'org.springframework.security:spring-security-test:5.6.2'
}

def props = new Properties()
file("src/main/resources/liquibase.properties").withInputStream {props.load(it)}

liquibase {
    activities {
        main {
            changeLogFile props.getProperty("changeLogFile")
            url props.getProperty("url")
            username props.getProperty("username")
            password props.getProperty("password")
            driver props.getProperty("driver")
        }
    }
}

test {
    useJUnit()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/gadelev/dto',
                    'com/gadelev/model',
                    'com/gadelev/security',
                    'com/gadelev/advice',
                    'com/gadelev/utils'
            ])
        }))
    }
}